---
title: shiftOut()
categories: [ "Functions" ]
subCategories: [ "Advanced I/O" ]
---

= shiftOut()


// OVERVIEW SECTION STARTS
[#overview]
--

[float]
=== 說明
在一個byte中一次位移一個bit的方移出資料。位移可以從最高有效位(如：最左)或是最小有效位(如：最右)。每一個bit時脈腳位電位有脈衝的寫入下一個bit的值，以此指示這個bit可以被運用。

如果您正在介接的裝置時脈觸發是要上升邊緣為準的話，第一次呼叫 `shiftOut()` 之前您必須要在時脈腳位電位是在低電位。如：先呼叫 `digitalWrite(clockPin, LOW)`。

註記：這是以軟體的方式實作。Arduino也有提供 link:https://www.arduino.cc/en/Reference/SPI[SPI library] 是以硬體的方式實作，硬體的方式運作較快速，但是只要在某些腳位上才能正常使用。
[%hardbreaks]


[float]
=== 語法
`shiftOut(dataPin, clockPin, bitOrder, value)`


[float]
=== 參數
`dataPin`: 欲一次位移讀出一個bit資料的腳位 (int)

`clockPin`: 能觸發告知 `dataPin` 值已正確寫入的腳位 (int)

`bitOrder`: 要以哪個方向填入資料。可能的值為 *MSBFIRST* 或是 *LSBFIRST* 。(最高有效位優先或是最小有效位優先)

`value`: 移出的值 (byte)

[float]
=== 回傳值
無

--
// OVERVIEW SECTION ENDS




// HOW TO USE SECTION STARTS
[#howtouse]
--

[float]
=== Example Code
// Describe what the example code is all about and add relevant code   ►►►►► THIS SECTION IS MANDATORY ◄◄◄◄◄
For accompanying circuit, see the https://arduino.cc/en/Tutorial/ShiftOut[tutorial on controlling a 74HC595 shift register].

[source,arduino]
----
//**************************************************************//
//  Name    : shiftOutCode, Hello World                         //
//  Author  : Carlyn Maw,Tom Igoe                               //
//  Date    : 25 Oct, 2006                                      //
//  Version : 1.0                                               //
//  Notes   : Code for using a 74HC595 Shift Register           //
//          : to count from 0 to 255                            //
//****************************************************************

//Pin connected to ST_CP of 74HC595
int latchPin = 8;
//Pin connected to SH_CP of 74HC595
int clockPin = 12;
////Pin connected to DS of 74HC595
int dataPin = 11;

void setup() {
  //set pins to output because they are addressed in the main loop
  pinMode(latchPin, OUTPUT);
  pinMode(clockPin, OUTPUT);
  pinMode(dataPin, OUTPUT);
}

void loop() {
  //count up routine
  for (int j = 0; j < 256; j++) {
    //ground latchPin and hold low for as long as you are transmitting
    digitalWrite(latchPin, LOW);
    shiftOut(dataPin, clockPin, LSBFIRST, j);
    //return the latch pin high to signal chip that it
    //no longer needs to listen for information
    digitalWrite(latchPin, HIGH);
    delay(1000);
  }
}
----
[%hardbreaks]

[float]
=== 密技與警告
dataPin和clockPin在呼叫之前就必須被設成輸出模式  link:../../digital-io/pinmode[pinMode()]

shiftOut 目前一次只輸出1byte(8 bits)，所以如果要輸出255以上的值，需要兩步驟才能完成。
[source,arduino]
----
// Do this for MSBFIRST serial
int data = 500;
// shift out highbyte
shiftOut(dataPin, clock, MSBFIRST, (data >> 8));
// shift out lowbyte
shiftOut(dataPin, clock, MSBFIRST, data);

// Or do this for LSBFIRST serial
data = 500;
// shift out lowbyte
shiftOut(dataPin, clock, LSBFIRST, data);
// shift out highbyte
shiftOut(dataPin, clock, LSBFIRST, (data >> 8));
----
[%hardbreaks]

--
// HOW TO USE SECTION ENDS
